plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
}

group = 'app.notesr.cli'
version = '1.0'
ext.defaultNoteSrVersion = '5.1'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.9'
        implementation 'org.slf4j:slf4j-simple:2.0.9'
        compileOnly 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.projectlombok:lombok:1.18.32'
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.mockito:mockito-inline:5.2.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.17.0'
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
        maxHeapSize = '1G'

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
        }
    }

    checkstyle {
        toolVersion = '10.12.4'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    tasks.matching { it.name == 'check' }.configureEach {
        dependsOn tasks.matching { t -> t.name == 'checkstyleMain' || t.name == 'checkstyleTest' }
    }
}

tasks.register('jacocoRootReport', JacocoReport) {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(subprojects.test)

    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    executionData.from = files(subprojects.jacocoTestReport.executionData)

    reports {
        html.required = true
        xml.required = true
    }
}

tasks.named('build') {
    dependsOn tasks.named('jacocoRootReport')
}

tasks.register("generateVersionProperties") {
    def outputDir = layout.buildDirectory.dir("generated-resources")

    outputs.dir outputDir
    doLast {
        def file = outputDir.get().file("version.properties").asFile
        file.parentFile.mkdirs()
        file.text = """
            utility.version=${project.version}
            noteSr.defaultVersion=${project.defaultNoteSrVersion}
        """.stripIndent().trim()
    }
}

tasks.named("processResources") {
    dependsOn("generateVersionProperties")
    from(layout.buildDirectory.dir("generated-resources")) {
        into "."
    }
}

tasks.register('copyCliBinaryToRoot', Copy) {
    dependsOn ':cli:installDist'

    from('cli/build/install/notesr-cli')
    into(layout.buildDirectory.dir("notesr-cli"))

    includeEmptyDirs = false
}

tasks.named('build') {
    dependsOn tasks.named('copyCliBinaryToRoot')
}
